apply plugin: 'com.android.application'
apply from: './dependencies.gradle'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "br.com.liveo.mvp"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "BASE_URL", "\"https://reqres.in/\"")
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        dev {
            storeFile file("../key/liveomvp.jks")
            storePassword "liveomvp"
            keyAlias "liveomvp"
            keyPassword "liveomvp"
        }
    }

    buildTypes {
        release {
            useProguard = true
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.dev
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            signingConfig signingConfigs.dev
        }

        android.applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "LiveoMVP-${variant.name}-${variant.versionName}.apk"
            }
        }
    }

    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res',
                    'src/main/res-screen/base',
                    'src/main/res-screen/home',
                    'src/main/res-screen/login',
                    'src/main/res-screen/splash',
            ]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}